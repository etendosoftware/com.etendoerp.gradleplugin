plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

def MAIN = "main"
def DEV = "dev"
def PROD = "prod"

final String URL_PROD = "https://maven.pkg.github.com/etendosoftware/com.etendoerp.gradleplugin"
final String URL_DEV = "https://repo.futit.cloud/repository/maven-public-snapshots"
final String PLUGIN_VERSION = "2.2.1"
final String PUBLICATION_TYPE = "-rc"

// Default mode
def mode = DEV

// If the version is updated, make sure to change it in the 'EtendoPlugin' class
def pluginVersion = PLUGIN_VERSION

static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

if (MAIN == gitBranch()) {
    mode = PROD
}
if (mode == DEV) {
    pluginVersion += "-SNAPSHOT"
}

def urlToPublish = (mode == PROD) ? URL_PROD : URL_DEV

group = 'com.etendoerp'
version(pluginVersion)

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = group
            version = pluginVersion
            artifactId = 'gradleplugin'
        }
    }
    repositories {
        maven {
            credentials {
                if (mode == PROD) {
                    username = "$githubUser"
                    password = "$githubToken"
                } else {
                    username = "$nexusUser"
                    password = "$nexusPassword"
                }
            }
            url(urlToPublish)
        }
    }
}

dependencies {
    implementation     gradleApi()
    testImplementation gradleTestKit()

    // https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'

    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    implementation 'org.postgresql:postgresql:42.5.4'

    // https://mvnrepository.com/artifact/com.oracle.database.jdbc/ojdbc8
    implementation 'com.oracle.database.jdbc:ojdbc8:21.6.0.0.1'

    // https://mvnrepository.com/artifact/org.jdom/jdom2
    implementation("org.jdom:jdom2:2.0.5") {
        exclude group: 'jdom', module: 'jdom'
    }

    // https://mvnrepository.com/artifact/jaxen/jaxen
    implementation("jaxen:jaxen:1.1.4") {
        exclude group: 'jdom', module: 'jdom'
    }

    testImplementation 'org.codehaus.groovy:groovy-all:3.0.22'
    testImplementation platform("org.spockframework:spock-bom:2.0-M4-groovy-3.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-junit4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.9.2'
    testImplementation 'org.junit.platform:junit-platform-suite-engine:1.9.2'
    testImplementation( "com.athaydes:spock-reports:2.0-groovy-3.0" ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

}

tasks.withType(Test).configureEach {
    // Using JUnitPlatform for running tests
    useJUnitPlatform()
}


test {
    maxHeapSize = "8192m"
    minHeapSize = "1024m"

    doFirst {
        file("$buildDir/heap-dumps").mkdirs()
    }

    jvmArgs = [
            '-XX:+UseG1GC',
            '-XX:+HeapDumpOnOutOfMemoryError',
            '-XX:+ExitOnOutOfMemoryError',
            '-XX:HeapDumpPath=./build/heap-dumps/heap.hprof',
            '-XX:MaxMetaspaceSize=1024m',
            '-XX:ReservedCodeCacheSize=256m',
            '-Dfile.encoding=UTF-8'
    ]

    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', true
    systemProperty 'nexusUser', project.hasProperty('nexusUser') ? project.nexusUser : System.getenv("NEXUS_USR")
    systemProperty 'nexusPassword', project.hasProperty('nexusPassword') ? project.nexusPassword : System.getenv("NEXUS_PSW")
    systemProperty 'githubUser', project.hasProperty('githubUser') ? project.githubUser : System.getenv("GITHUB_USER")
    systemProperty 'githubToken', project.hasProperty('githubToken') ? project.githubToken : System.getenv("GITHUB_TOKEN")
    systemProperty 'test.bbdd.sid', project['test.bbdd.sid']
    systemProperty 'test.bbdd.systemUser', project['test.bbdd.systemUser']
    systemProperty 'test.bbdd.systemPassword', project['test.bbdd.systemPassword']
    systemProperty 'test.bbdd.user', project['test.bbdd.systemUser']
    systemProperty 'test.bbdd.password', project['test.bbdd.systemPassword']
    systemProperty 'test.bbdd.url', project['test.bbdd.url']
    systemProperty 'org.gradle.jvmargs', '-Dfile.encoding=UTF-8'
    systemProperty 'etendoCoreName', project.hasProperty('etendoCoreName') ? project.etendoCoreName : "etendo-core"
    systemProperty 'etendoCoreVersion', project.property('etendoCoreVersion')
    environment 'JAVA_HOME', System.getProperties().get("java.home")

}

task upgradePluginVersion() {
    final String MAJOR = 'major'
    final String MINOR = 'minor'
    final String PATCH = 'patch'
    doLast {
        def typeVersion
        if (project.hasProperty('type')){
            typeVersion = project.getProperty('type')
        } else {
            throw new GradleException("The parameter -Ptype is required")
        }

        if (typeVersion != MAJOR && typeVersion != MINOR && typeVersion != PATCH) {
            throw new GradleException("The parameter value '${typeVersion}' is not valid, the options should be 'major', 'minor' or 'patch'")
        }

        def versionParts = PLUGIN_VERSION.trim().split("\\.")
        if (versionParts.length != 3) {
            throw new GradleException("The version must be in the format major.minor.patch")
        }

        int major = versionParts[0].toInteger()
        int minor = versionParts[1].toInteger()
        int patch = versionParts[2].toInteger()

        if (typeVersion == MAJOR) {
            major++
            minor = 0
            patch = 0
        } else if (typeVersion == MINOR) {
            minor++
            patch = 0
        } else if (typeVersion == PATCH) {
            patch++
        }

        println "Version: ${major}.${minor}.${patch}"
        def nextVersion = "${major}.${minor}.${patch}"

        //Rewrite build.gradle file with new value
        def buildGradleContent = file('build.gradle').text
        buildGradleContent = buildGradleContent.replaceAll(/PLUGIN_VERSION = \"${PLUGIN_VERSION}\"/, "PLUGIN_VERSION = \"${nextVersion}\"")
        file('build.gradle').write(buildGradleContent)

        //Rewrite EtendoPlugin.groovy file with new value
        def etendoPluginContent = file('src/main/groovy/com/etendoerp/EtendoPlugin.groovy').text
        etendoPluginContent = etendoPluginContent.replaceAll(/PLUGIN_VERSION = \"\d+.\d+.\d+\"/, "PLUGIN_VERSION = \"${nextVersion}\"")
        file('src/main/groovy/com/etendoerp/EtendoPlugin.groovy').write(etendoPluginContent)
    }
}

gradlePlugin {
    plugins {
        etendoPlugin {
            id = "com.etendoerp.gradleplugin"
            implementationClass = "com.etendoerp.EtendoPlugin"
        }
    }
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()
}

task jacocoRootReport(type: JacocoReport) {
    executionData fileTree(dir: "$buildDir/jacoco", include: "*.exec")
    sourceDirectories.setFrom(files("src/main/groovy"))
    classDirectories.setFrom(fileTree(dir: "$buildDir/classes/groovy/main", include: '**/*.class'))

    reports {
        xml.required = true
        html.required = true
    }
}

test.onlyIf { true }

